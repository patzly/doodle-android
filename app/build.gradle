/*
 * This file is part of Doodle Android.
 *
 * Doodle Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Doodle Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Doodle Android. If not, see <http://www.gnu.org/licenses/>.
 *
 * Copyright (c) 2019-2025 by Patrick Zedler
 */

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.navigation.safeargs
}

android {
    namespace 'xyz.zedler.patrick.doodle'
    compileSdk 35

    defaultConfig {
        applicationId 'xyz.zedler.patrick.doodle'
        minSdk 21
        targetSdk 35
        versionCode 42
        versionName '6.0.0'
        resourceConfigurations += ['ar', 'bqi', 'cs', 'da', 'de', 'en', 'es', 'fi', 'fil', 'fr',
                                   'hu', 'in', 'it', 'iw', 'ja', 'ko', 'lt', 'nl', 'pl', 'pt-rBR',
                                   'ro', 'ru', 'tr', 'ug', 'uk', 'vi', 'zh-rCN']
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
                proguardFiles {
                    getDefaultProguardFile('proguard-android-optimize.txt')
                    'proguard-rules.pro'
                }
            }
            debuggable false
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    lint {
        abortOnError false
        disable 'MissingTranslation'
    }

    bundle {
        storeArchive {
            enable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_17
    }

    dependenciesInfo {
        includeInApk false
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.fragment
    implementation libs.navigation.fragment
    implementation libs.navigation.ui
    implementation libs.preference
    implementation libs.material
}
